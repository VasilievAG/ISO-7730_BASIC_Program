10  REM компьютерная программа (BASIC) для вычисления
20  REM прогнозируемой средней оценки (PMV) и прогнозируемого процента недовольных (PPD)
30  REM в соответствии с международным стандартом ISO 7730
40  CLS : PRINT "ВВОД ДАННЫХ"                            : REM ввод данных
50  INPUT "  Одежда                             (кло)     ", CLO
60  INPUT "  Скорость обмена веществ            (мет)     ", MET
70  INPUT "  Наружная работа, обычно рядом с 0  (мет)     ", WM
80  INPUT "  Температура воздуха                (град.С)  ", TA
90  INPUT "  Средняя температура излучения      (град.С)  ", TR
100 INPUT "  Средняя скорость движения воздуха  (м/с)     ", VEL
110 PRINT "  ВВЕДИТЕ ИЛИ ВЛАЖНОСТЬ ИЛИ ДАВЛЕНИЕ ВОДЯНОГО ПАРА, НО НЕ ОБА"
120 INPUT "  Относительная влажность            (%)       ", RH
130 INPUT "  Давление водяного пара             (Па)      ", PA
140 REM давление насыщенного водяного пара при температуре Т в кПа = EXP(16.6536-4030.183/(T+235))
145 e=2.718281828459045
150 IF PA=0 THEN PA=RH*10*(e^(16.6536-4030.183/(TA+235))): REM давление водяного пара, Па
160 ICL=.155*CLO                                         : REM термоизоляция одежды, кв.м*К/Вт
170 M=MET*58.15                                          : REM скорость обмена веществ, Вт/кв.м
180 W=WME*58.15                                          : REM наружная работа, Вт/кв. м
190 MW=M-W                                               : REM внутреннее производство тепла в теле человека
200 IF ICL<=0.078 THEN FCL=1+1.29*ICL ELSE FCL=1.05+0.645*ICL 
201 REM ICL - множитель площади одежды
210 HCF=12.1*SQR(VEL)                                    : REM коэффициент теплообмена при принудительной конвекции
220 TAA=TA+273                                           : REM температура воздуха, К
230 TRA=TR+273                                           : REM средняя температура излучения, К
240 REM ------------------ВЫЧИСЛЕНИЕ ТЕМПЕРАТУРЫ ПОВЕРХНОСТИ ОДЕЖДЫ ПОСЛЕДОВАТЕЛЬНЫМИ ПРИБЛИЖЕНИЯМИ-----------
250 TCLA=TAA+(35.5-TA)/(3.5*ICL+.1)                      : REM первое предположение о температуре поверхности одежды
310 XN=TCLA
320 XF=XN
330 N=0                                                  : REM номер итерации
340 EPS=.0000015                                         : REM критерий завершения цикла поиска приближенного значения
350 XF=(XF+XN)/2
360 HCN=2.38*(ABS(XF-TAA)^.25)                           : REM коэффициент теплообмена при естественной конвекции
370 IF HCF>HCN THEN HC=HCF ELSE HC = HCN
380 P1=ICL*FCL*((3.96*(10^-8)*((XF^4)-(TRA^4)))+(HC*(XF-TAA))
381 XN=35.7-(.028*MW)-P1+273
390 N=N+1
400 IF N>150 THEN GOTO 550
410 IF ABS(XN-XF)>EPS THEN GOTO 350
420 TCL=XN-273                                           : REM температура поверхности одежды
430 REM ------------------------ВЫЧИСЛЕНИЕ КОМПОНЕНТОВ ПОТЕРИ ТЕПЛА------------------------
440 HL1=3.05*.001*(5733-6.99*MW-PA)
450 IF MW>58.15 THEN HL2=.42*(MW-58.15) ELSE HL2=0!
451                                                        REM потеря тепла за счет потения (комфортного)
460 HL3=1.7*.00001*M*(5867-PA)                           : REM скрытая потеря тепла через дыхание
470 HL4=.0014*M*(34-TA)                                  : REM потеря тепла из-за сухого дыхания
480 HL5=3.96*(10^-8)*FCL*(XN^4-TRA^4)                    : REM потеря тепла из-за излучения
490 HL6 = FCL*HC*(TCL-TA)
500 REM ------------------------ВЫЧИСЛЕНИЕ PMV И PPD------------------------
510 TS=.303*(e^(-.036*M))+.028                           : REM коэффициент переноса температурной чувствительности
520 PMV=TS*(MW-HL1-HL2-HL3-HL4-HL5-HL6)                  : REM прогнозируемая средняя оценка
530 PPD=100-95*(e^(-.03353*(PMV^4)-.2179*(PMV^2)))       : REM прогнозируемый процент недовольных
540 GOTO 570
550 PMV=999999!
560 PPD=100
570 PRINT : PRINT "РЕЗУЛЬТАТ"                            : REM вывод результата
580 PRINT "прогнозируемая средняя оценка (PMV):", PMV
590 PRINT "прогнозируемый процент недовольных (PPD):", PPD